  TITLE, "FACET2 scavenger e- (December 9, 2022)"

! *** OPTICS=FACET2-09DEC22 ***

  ASSIGN, PRINT="FACET2s.print"
  ASSIGN, ECHO="FACET2s.echo"
  OPTION, -ECHO, INFO, WARN, VERIFY

! ==============================================================================
! Modification History
! ------------------------------------------------------------------------------
! 18-MAY-2021, G. White
!  * fixed LI19 corrector locations, removed 19-8a and updated S20 experiment
!    object locations @ Q0-2D quad locations
!  * added Q0FF, Q1FF, Q2FF, removed QFF4, locations according to metrology
!    measurements
! ------------------------------------------------------------------------------
! 06-MAR-2019, M. Woodley
!  * from FACET2e.mad8 ... simple geometry matched
! ------------------------------------------------------------------------------
! 13-SEP-2018, M. Woodley
!  * from FACET2e.mad8 ... geometry not rematched
! ------------------------------------------------------------------------------
! 08-MAR-2018, M. Woodley
!  * from FACET2e.mad8
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
! element and line definitions
! ------------------------------------------------------------------------------

  CALL, FILENAME="FACET2e_master.xsif"

! ------------------------------------------------------------------------------
! BETA0 block definitions
! ------------------------------------------------------------------------------

  TWi  : BETA0, BETX=BXi,  ALFX=AXi,  BETY=BYi,  ALFY=AYi
  TW19 : BETA0, BETX=BX19, ALFX=AX19, BETY=BY19, ALFY=AY19

! ------------------------------------------------------------------------------
! sigma matrix definitions
! ------------------------------------------------------------------------------

! definition 1): original design beam (from SCAV20A.trns)

  COMMENT !at exit face of Q19701 [Z=1902.8394]
    Es   := 33.0 !GeV
    GAMs := Es/EMASS

    rS11s :=  0.08830e-3 !m
    rS22s :=  0.00688e-3 !rad
    r12s  :=  0.6447
    rS33s :=  0.13218e-3 !m
    rS44s :=  0.00651e-3 !rad
    r34s  := -0.8418
    rS55  :=  1.0e-3     !m
    rS66  :=  1.0e-2     !1

    S11s    := rS11s*rS11s
    S22s    := rS22s*rS22s
    S12s    := rS11s*rS22s*r12s
    EmitXs  := SQRT(S11s*S22s-S12s*S12s)
    EmitXns := GAMs*EmitXs
    S33s    := rS33s*rS33s
    S44s    := rS44s*rS44s
    S34s    := rS33s*rS44s*r34s
    EmitYs  := SQRT(S33s*S44s-S34s*S34s)
    EmitYns := GAMs*EmitYs

    BXs  := S11s/EmitXs             !19.081458070208
    AXs  := -r12s/SQRT(1-r12s*r12s) !-0.775773306063
    DXs  := 0
    DPXs := 0
    BYs  := S33s/EmitYs             !41.718922338
    AYs  := -r34s/SQRT(1-r34s*r34s) ! 0.878409355683
    DYs  := 0
    DPYs := 0

    VALUE, EmitXns,EmitXs,BXs,AXs
    VALUE, EmitYns,EmitYs,BYs,AYs
  ENDCOMMENT

! definition 2): FACET-II scavenger beam

 !COMMENT !at MSCAVEXT 
    Es   := E19
    GAMs := Es/EMASS

    EmitXns :=  4.0e-6
    EmitXs  := EmitXns/GAMs
    BXs     := BX19
    AXs     := AX19
    DXs     :=  0
    DPXs    :=  0
    EmitYns :=  4.0e-6
    EmitYs  := EmitYns/GAMs
    BYs     := BY19
    AYs     := AY19
    DYs     :=  0
    DPYs    :=  0

    S11s := EmitXs*BXs
    S22s := EmitXs*(1+AXs*AXs)/BXs
    S12s := -EmitXs*AXs
    S33s := EmitYs*BYs
    S44s := EmitYs*(1+AYs*AYs)/BYs
    S34s := -EmitYs*AYs

    rS11s := SQRT(S11s)
    rS22s := SQRT(S22s)
    r12s  := S12s/(rS11s*rS22s)
    rS33s := SQRT(S33s)
    rS44s := SQRT(S44s)
    r34s  := S34s/(rS33s*rS44s)
    rS55  := 1.0e-3
    rS66  := 1.0e-2
 !ENDCOMMENT

! ------------------------------------------------------------------------------

! beam

  SIGs : SIGMA0, SIGX=rS11s, SIGPX=rS22s, R21=r12s, &
                 SIGY=rS33s, SIGPY=rS44s, R43=r34s, &
                 SIGT=rS55, SIGPT=rS66

  BEAM0s : BEAM, PARTICLE=ELECTRON, ENERGY=Es, EX=EmitXs, EY=EmitYs, &
                 SIGT=rS55, SIGE=rS66, NPART=2.5e+10 !4 nC
! Twiss

  TWs : BETA0, BETX=BXs, ALFX=AXs, DX=DXs, DPX=DPXs, &
               BETY=BYs, ALFY=AYs, DY=DYs, DPY=DPYs

! ------------------------------------------------------------------------------
! commands
! ------------------------------------------------------------------------------

  SETPLOT, XSIZE=25.4, YSIZE=20.32
  SETPLOT, LWIDTH=5, LSCALE=1.5, SSCALE=1.5, RSCALE=1.5
  OPTION, ECHO

 !CALL, FILENAME="FACET2s_match.mad8"
 !STOP

! ------------------------------------------------------------------------------
! original SCAV20A optics
! - uncomment 1st sigma matrix definition above
! ------------------------------------------------------------------------------

  COMMENT
    CALL, FILENAME="SCAV20A.xsif" !original EP01 magnet settings
    BEAM0s
    USE, SCAV20A
    PRINT, FULL
    TWISS, BETA0=TWs, SAVE
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, FILE="FACET2s"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, FILE="FACET2s"
    BEAM0s
    USE, SCAV20A
    PRINT, FULL
    SELECT, OPTICS, FULL
    ENVELOPE, SIGMA0=SIGs, TAPE="FACET2s_envelope.tape"
  ENDCOMMENT

! ------------------------------------------------------------------------------
! FACET-II extraction
! - uncomment 2nd sigma matrix definition above
! - use MGEO1 match to set LI19 kicker and Q19701 kicks
! - use offAxisQuad.m to compute initial bend angles for BKY170, Q19501,
!   Q19601, and Q19701
! - use MGEO2 match to fine-tune BKY170 and Q19701 bend angles
! - use MTWs match to set scavenger line magnets
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
! start at entrance to LI19 kicker
! - uncomment 2nd sigma matrix definition above
! ------------------------------------------------------------------------------

  COMMENT
    BEAM, ENERGY=E19
    USE, SCAV
    PRINT, FULL
    SURVEY, Z0=Z19, TAPE="FACET2s_survey.tape"
  ENDCOMMENT

  COMMENT
    BEAM, ENERGY=E19
    USE, SCAV
    PRINT, FULL
    TWISS, COUPLE, BETX=BX19, ALFX=AX19, BETY=BY19, ALFY=AY19, SAVE, &
      TAPE="FACET2s_twiss.tape", RTAPE="FACET2s_rmat.tape"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, FILE="FACET2s"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, FILE="FACET2s"
  ENDCOMMENT

  COMMENT
    BEAM, ENERGY=E19
    USE, SCAV
    PRINT, FULL
    SELECT, OPTICS, FULL
    ENVELOPE, SIGMA0=SIGs, TAPE="FACET2s_envelope.tape"
  ENDCOMMENT

! ------------------------------------------------------------------------------

 !COMMENT
    BEAM, ENERGY=E0
    USE, F2_SCAV
    PRINT, FULL
    SURVEY, TAPE="FACET2s_survey.tape", &
      X0=Xc, Y0=Yc, Z0=Zc, THETA0=THETAc, PHI0=PHIc, PSI0=PSIc
    BEAM, ENERGY=Ei
    USE, FACET2s
    PRINT, FULL
    TWISS, BETA0=TWi, SAVE, TAPE="FACET2s_twiss.tape"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=ENERGY, &
      STYLE=100, SPLINE=.F., FILE="FACET2s"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE=.T., FILE="FACET2s"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, SPLINE=.F., FILE="FACET2s"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=LI19BEG/ENDSCAV, FILE="FACET2s", &
      TITLE="LI19 + Scavenger Extraction"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, RANGE=LI19BEG/ENDSCAV, FILE="FACET2s", &
      TITLE="LI19 + Scavenger Extraction"
 !ENDCOMMENT

! ------------------------------------------------------------------------------

  STOP
