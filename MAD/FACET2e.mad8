  TITLE, "FACET2 e-"

! *** OPTICS=FACET2-preRelease ***

  ASSIGN, PRINT="FACET2e.print"
  ASSIGN, ECHO="FACET2e.echo"
  OPTION, -ECHO, INFO, WARN, VERIFY

! ==============================================================================
! Modification History
! ------------------------------------------------------------------------------
! 06-SEP-2023, M. Woodley
!  * fix BC14E/P drifts
!  * set all QUAD's and SEXT's to Lucretia values
! ------------------------------------------------------------------------------
! 18-MAY-2021, G. White
!  * fixed LI19 corrector locations, removed 19-8a and updated S20 experiment
!    object locations @ Q0-2D quad locations
!  * added Q0FF, Q1FF, Q2FF, removed QFF4, locations according to metrology
!    measurements
! ------------------------------------------------------------------------------
! 13-NOV-2017, M. Woodley
!  * move CATHODE closer to L0; remove BPM3F
! ------------------------------------------------------------------------------
! 05-SEP-2017, M. Woodley
!  * tweak XLL, ZLL, and LINJ to get correct SURVEY coordinates
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
! element and line definitions
! ------------------------------------------------------------------------------

  CALL, FILENAME="FACET2e_master.xsif"

! ------------------------------------------------------------------------------
! BETA0 block definitions
! ------------------------------------------------------------------------------

  TW0: BETA0, BETX=BX0, ALFX=AX0, BETY=BY0, ALFY=AY0
  TWi: BETA0, BETX=BXi, ALFX=AXi, BETY=BYi, ALFY=AYi

! misc

  TWm : BETA0
  TWf : BETA0

! ------------------------------------------------------------------------------
! BEAM definition (from FACET2e_baseline.mat)
! ------------------------------------------------------------------------------

  BEAM0: BEAM, ENERGY=E0, NPART=Q0/QELEC
  BEAM, ENERGY=Ei, NPART=Q0/QELEC, &
        EXN=3.0e-6, EYN=3.0e-6, &
        SIGT=0.738520173493e-3, SIGE=0.186678021326e-3

! ------------------------------------------------------------------------------
! commands
! ------------------------------------------------------------------------------

  SETPLOT, XSIZE=25.4, YSIZE=20.32
  SETPLOT, LWIDTH=5, LSCALE=1.5, SSCALE=1.5, RSCALE=1.5, ASCALE=1.5
  OPTION, ECHO

 !CALL, FILENAME="FACET2e_match.mad8"
 !CALL, FILENAME="FACET2e_makeSymbols.mad8"
 !CALL, FILENAME="FACET2e_makeElegant.mad8"

! ------------------------------------------------------------------------------

 !STOP

! ------------------------------------------------------------------------------

  COMMENT !for testing the Matlab model
    BEAM, ENERGY=E0
    USE, F2_ELEC
    PRINT, FULL
    TWISS, BETA0=TW0, TAPE="FACET2e_twiss.tape"
    BEAM, ENERGY=E0
    USE, F2_ELEC_PAX
    PRINT, FULL
    TWISS, BETA0=TW0, TAPE="FACET2e_PAX_twiss.tape"
    BEAM, ENERGY=E0
    USE, F2_SCAV
    PRINT, FULL
    TWISS, BETA0=TW0, TAPE="FACET2s_twiss.tape"
    BEAM, ENERGY=Ei
    USE, FACET2e
    PRINT, FULL
    TWISS, BETA0=TWi, TAPE="FACET2ei_twiss.tape"
    BEAM, ENERGY=Ei
    USE, FACET2e_PAX
    PRINT, FULL
    TWISS, BETA0=TWi, TAPE="FACET2e_PAXi_twiss.tape"
    BEAM, ENERGY=Ei
    USE, FACET2s
    PRINT, FULL
    TWISS, BETA0=TWi, TAPE="FACET2si_twiss.tape"
    STOP
  ENDCOMMENT

! ------------------------------------------------------------------------------

 !COMMENT !standard output
    BEAM, ENERGY=E0
    USE, F2_ELEC
    PRINT, FULL
    SURVEY, TAPE="FACET2e_survey.tape", &
      X0=Xc, Y0=Yc, Z0=Zc, THETA0=THETAc, PHI0=PHIc, PSI0=PSIc
    BEAM, ENERGY=Ei
    USE, FACET2e
    PRINT, FULL
    comment
      SAVEBETA, TWLH, HTRUNDF
      SAVEBETA, TW10, PR10571
      SAVEBETA, TW11, BC11CEND
      SAVEBETA, TW14, ENDBC14E
      SAVEBETA, TW19, MSCAVEXT
      SAVEBETA, TW20, BEGBC20
      SAVEBETA, TWip, MIP
    endcomment
    TWISS, BETA0=TWi, SAVE, TAPE="FACET2e_twiss.tape"
    comment
      SHOW, TWLH
      SHOW, TW10
      SHOW, TW11
      SHOW, TW14
      SHOW, TW19
      SHOW, TW20
      SHOW, TWip
    endcomment
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=ENERGY, &
      STYLE=100, -SPLINE, RANGE=#S/BEGBC20, FILE="FACET2e", &
      TITLE="Injector + Linac"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=#S/BEGBC20, FILE="FACET2e", &
      TITLE="Injector + Linac"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, RANGE=#S/BEGBC20, FILE="FACET2e", &
      TITLE="Injector + Linac"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=BEGBC20/ENDBC20, FILE="FACET2e", &
      TITLE="BC20"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, RANGE=BEGBC20/ENDBC20, FILE="FACET2e", &
      TITLE="BC20"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=ENDBC20/MAINDUMP, FILE="FACET2e", &
      TITLE="Final Focus + Spectrometer"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, RANGE=ENDBC20/MAINDUMP, FILE="FACET2e", &
      TITLE="Final Focus + Spectrometer"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=#S/#E, FILE="FACET2e"

    BEAM, ENERGY=E0
    USE, F2_ELEC_PAX
    PRINT, FULL
    SURVEY, TAPE="FACET2e_PAX_survey.tape", &
      X0=Xc, Y0=Yc, Z0=Zc, THETA0=THETAc, PHI0=PHIc, PSI0=PSIc
    BEAM, ENERGY=Ei
    USE, FACET2e_PAX
    PRINT, FULL
    TWISS, BETA0=TWi, SAVE, TAPE="FACET2e_PAX_twiss.tape"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=ENDBC20/MAINDUMP, FILE="FACET2e_PAX", &
      TITLE="Final Focus + Spectrometer"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      STYLE=100, -SPLINE, RANGE=ENDBC20/MAINDUMP, FILE="FACET2e_PAX", &
      TITLE="Final Focus + Spectrometer"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      STYLE=100, SPLINE, RANGE=#S/#E, FILE="FACET2e_PAX"
 !ENDCOMMENT

! ------------------------------------------------------------------------------

  STOP

! ------------------------------------------------------------------------------
! FACET2e working stuff
! ------------------------------------------------------------------------------

  COMMENT !standard output
    BEAM, ENERGY=E0
    USE, F2_ELEC
   !VALUE, DM23A[L]
   !VALUE, DM23B[L]
   !VALUE, DM23C[L]
   !VALUE, DM24A[L]
    VALUE, DM24B[L]
    MATCH, SURVEY, XS=Xc, YS=Yc, ZS=Zc, THETAS=THETAc, PHIS=PHIc, PSIS=PSIc
     !VARY, DM23A[L], STEP=1.E-6
     !VARY, DM23B[L], STEP=1.E-6
     !VARY, DM23C[L], STEP=1.E-6
     !VARY, DM24A[L], STEP=1.E-6
      VARY, DM24B[L], STEP=1.E-6
      WEIGHT, ZS=1
     !CONSTR, VV14887,  ZS=1420.916000+0.0759
     !CONSTR, BL14888,  ZS=1421.030235+0.1524
     !CONSTR, BPM14891, ZS=1421.311100+0.0472
     !CONSTR, IM14890,  ZS=1421.671000-0.1095
      CONSTR, BPM14901, ZS=1421.973100-0.2048
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
   !VALUE, DM23A[L]
   !VALUE, DM23B[L]
   !VALUE, DM23C[L]
   !VALUE, DM24A[L]
    VALUE, DM24B[L]
    PRINT, FULL
    SURVEY, TAPE="FACET2e_survey.tape", &
      X0=Xc, Y0=Yc, Z0=Zc, THETA0=THETAc, PHI0=PHIc, PSI0=PSIc
  ENDCOMMENT

! ------------------------------------------------------------------------------

  comment
    BEAM, ENERGY=E20
   !SET, ABP, 0
    USE, PAXCHICANE
    PRINT, FULL
    SURVEY, TAPE="PAX_survey.tape" !, Z0=2002.886872
  endcomment

  comment
    BEAM, ENERGY=E0
    USE, F2_ELEC
   !VALUE, D2FFa[L],D2FFb[L],D2FF[L],D2FFa[L]+D2FFb[L]
   !VALUE, D1FFa[L],D1FFb[L],D1FF[L],D1FFa[L]+D1FFb[L]
   !VALUE, DEX20_2[L],DEX20_3[L],DEX20_2[L]+DEX20_3[L]
   !VALUE, DEX20_3[L],DEX20_4[L],DEX20_3[L]+DEX20_4[L]
    VALUE, D3Dp[L],D4Dp[L],D3Dp[L]+D4Dp[L],ZB2Bo
    MATCH, SURVEY, &
      XS=Xc, YS=Yc, ZS=Zc, THETAS=THETAc, PHIS=PHIc, PSIS=PSIc
     !VARY, D2FFa[L], STEP=1.E-6
     !VARY, D1FFa[L], STEP=1.E-6
      VARY, D3Dp[L],  STEP=1.E-9
      VARY, ZB2Bo,    STEP=1.E-9
      WEIGHT, ZS=1
     !CONSTR, YC4FF,      ZS=1988.752
     !CONSTR, XC4FF,      ZS=1990.332
     !CONSTR, BEWIN1,     ZS=1991.790
     !CONSTR, IQMON20,    ZS=1991.850
      CONSTR, BCX203291a, ZS=2002.7536317
      CONSTR, BCX203298a, ZS=2003.2743317
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
   !VALUE, D2FFa[L],D2FFb[L],D2FF[L],D2FFa[L]+D2FFb[L]
   !VALUE, D1FFa[L],D1FFb[L],D1FF[L],D1FFa[L]+D1FFb[L]
   !VALUE, DEX20_2[L],DEX20_3[L],DEX20_2[L]+DEX20_3[L]
   !VALUE, DEX20_3[L],DEX20_4[L],DEX20_3[L]+DEX20_4[L]
    VALUE, D3Dp[L],D4Dp[L],D3Dp[L]+D4Dp[L],ZB2Bo
    PRINT, FULL
    SURVEY, TAPE="survey.tape", &
      X0=Xc, Y0=Yc, Z0=Zc, THETA0=THETAc, PHI0=PHIc, PSI0=PSIc
  endcomment

! ------------------------------------------------------------------------------

  STOP
